#!/usr/bin/env gnuplot -c

# Output W3C Scalable Vector Graphics
set terminal svg
set output "results.svg"

# Read comma-delimited data from file
set datafile separator comma

# Set graph title
set label center at graph 0.5, char 1 "last updated on ".strftime("%b %d, %Y at %H:%M:%S UTC", time(0))." using ".zsh_version

# Set label of y-axis
set ylabel 'startup time in seconds'

# Set y-axis range to start at 0
set yrange [0:]

# Disable the tiny tick lines on both axis
set xtics scale 0
set ytics scale 0

# Disable legend
set key off

# White background
set style rectangle fillstyle noborder
set object 1 rectangle behind from screen 0,0 to screen 1,1 fillcolor rgb "white"

set bmargin 4.5
set boxwidth 0.5
set style fill solid
plot '<(grep -v vanilla results/results.csv | sed "s/-/\\\n/" | sort -t, -g -k2)' using 2: xtic(1) with boxes linecolor rgb "#dc1e3d"
